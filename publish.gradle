task sourcesJar(type: Jar, dependsOn: classes) {

    classifier = 'sources'

    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {

    classifier = 'javadoc'

    from javadoc.destinationDir
}

artifacts {

    archives sourcesJar

    archives javadocJar
}

def pomConfig = {

    licenses {

        license {

            name 'GNU General Public License, Version 3.0'

            url 'https://www.gnu.org/licenses/gpl-3.0.txt'

            distribution "repo"
        }
    }

    developers {

        developer {

            id 'filkra'

            name 'Filip Krakowski'

            email 'Filip.Krakowski@Uni-Duesseldorf.de'
        }
    }

    scm {

        url 'https://github.com/hhu-bsinfo/dxgradle'
    }
}

publishing {

    publications {

        mavenPublication(MavenPublication) {

            from components.java

            artifact sourcesJar {

                classifier 'sources'
            }

            artifact javadocJar {

                classifier 'javadoc'
            }

            groupId project.group

            artifactId project.name

            version project.version

            pom.withXml {

                def root = asNode()

                root.appendNode('description', 'DXRam\'s custom Gradle plugin')

                root.appendNode('name', 'DXGradle')

                root.appendNode('url', 'https://github.com/hhu-bsinfo/dxgradle')

                root.children().last() + pomConfig
            }
        }
    }
}

bintray {

    user = System.getProperty('bintray.user')

    key = System.getProperty('bintray.key')

    publications = ['mavenPublication']

    pkg {

        repo = 'DXGradle'

        name = 'dxgradle'

        licenses = ['GPL-3.0']

        vcsUrl = 'https://github.com/hhu-bsinfo/dxgradle.git'

        version {

            name = project.version

            released  = new Date()
        }
    }

}