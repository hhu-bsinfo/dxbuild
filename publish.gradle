def pomConfig = {
    licenses {
        license {
            name 'GNU General Public License, Version 3.0'
            url 'https://www.gnu.org/licenses/gpl-3.0.txt'
            distribution "repo"
        }
    }

    developers {
        developer {
            id 'filkra'
            name 'Filip Krakowski'
            email 'filip.krakowski@uni-duesseldorf.de'
        }
    }

    scm {
        url 'https://github.com/hhu-bsinfo/dxbuild'
    }
}

publishing {

    publications {

        mavenPublication(MavenPublication) {

            from components.java

            artifact sourcesJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }

            groupId project.group
            artifactId project.name
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'DXRam\'s custom Gradle plugin')
                root.appendNode('name', 'DXBuild')
                root.appendNode('url', 'https://github.com/hhu-bsinfo/dxbuild')
                root.children().last() + pomConfig
            }
        }
    }
}