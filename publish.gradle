def pomConfig = {
    licenses {
        license {
            name 'GNU General Public License, Version 3.0'
            url 'https://www.gnu.org/licenses/gpl-3.0.txt'
            distribution "repo"
        }
    }

    developers {
        developer {
            id 'filkra'
            name 'Filip Krakowski'
            email 'Filip.Krakowski@Uni-Duesseldorf.de'
        }
    }

    scm {
        url 'https://github.com/hhu-bsinfo/dxgradle'
    }
}

publishing {

    publications {

        mavenPublication(MavenPublication) {

            from components.java

            artifact sourcesJar {
                classifier 'sources'
            }

            artifact javadocJar {
                classifier 'javadoc'
            }

            groupId project.group
            artifactId project.name
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'DXRam\'s custom Gradle plugin')
                root.appendNode('name', 'DXGradle')
                root.appendNode('url', 'https://github.com/hhu-bsinfo/dxgradle')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getProperty('bintray.user')
    key = System.getProperty('bintray.key')
    publications = ['mavenPublication']
    publish = true
    override = true

    pkg {
        repo = 'dxram'
        name = 'dxgradle'
        userOrg = 'hhu-bsinfo'
        licenses = ['GPL-3.0']
        websiteUrl = 'https://www.dxram.io'
        vcsUrl = 'https://github.com/hhu-bsinfo/dxgradle.git'
        labels = ['gradle', 'plugin', 'dxram']
        githubRepo = 'hhu-bsinfo/dxgradle'
        //githubReleaseNotesFile = 'README.md'

        version {
            name = project.version
            released  = new Date()
        }
    }
}